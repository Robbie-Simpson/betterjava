plugins {
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
    id "jacoco"
}

apply from: "$rootDir/gradle/versioning.gradle"

group = 'com.revolut'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'jar'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    testCompile "org.junit.jupiter:junit-jupiter:5.4.2"
    testCompile "org.assertj:assertj-core:3.11.1"
}

tasks.withType(JavaCompile) {
    options.incremental = true
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "short"

        debug {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.revolut'
            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true

    pkg {
        userOrg = 'revolut'
        repo = 'revolut'
        name = 'betterjava'
        desc = 'Better Java Microtype Lib'
        websiteUrl = 'https://github.com/Revolut-Engineering/betterjava'
        issueTrackerUrl = 'https://github.com/Revolut-Engineering/betterjava/issues'
        vcsUrl = 'https://github.com/Revolut-Engineering/betterjava.git'
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        labels = ['optional', 'microtype', 'java', 'core']

        version {
            name = "${project.version}"
            desc = "Better Java Lib ${project.version}"
            vcsTag = "${project.version}"
            released = new Date()
        }
    }
}

wrapper {
    gradleVersion = "5.4.1"
    distributionType = Wrapper.DistributionType.ALL
}